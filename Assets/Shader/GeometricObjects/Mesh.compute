#ifndef MESH
#define MESH
#include "Utilitie.compute"
#include "Material.compute"

StructuredBuffer<float3> _Vertices;
uint VerticeNum = 0;
StructuredBuffer<int> _Indices;
uint IndiceNum = 0;
StructuredBuffer<float3> _Normals;
uint NormalNum;

struct Mesh
{
	float4x4 localToWorldMatrix;
	//localToWorldMatrix的逆反
	float4x4 localNormalToWorldMatrix;
	int indices_offset;
	int indices_count;


	static void Init()
	{
		uint temp;
		_Vertices.GetDimensions(VerticeNum, temp);
		_Indices.GetDimensions(IndiceNum, temp);
		_Normals.GetDimensions(NormalNum, temp);
	}
	void Hit(Ray ray, inout RayHit bestHit)
	{
		uint indices_tail = indices_offset + indices_count;
		for (uint i = indices_offset; i < indices_tail; i += 3)
		{
			float3 v0 = (mul(localToWorldMatrix,
				float4(_Vertices[_Indices[i]], 1))).xyz;
			float3 v1 = (mul(localToWorldMatrix,
				float4(_Vertices[_Indices[i + 1]], 1))).xyz;
			float3 v2 = (mul(localToWorldMatrix,
				float4(_Vertices[_Indices[i + 2]], 1))).xyz;
			float3 n0 = normalize((mul(localNormalToWorldMatrix,
				float4(_Normals[_Indices[i]], 0))).xyz);
			float3 n1 = normalize((mul(localNormalToWorldMatrix,
				float4(_Normals[_Indices[i + 1]], 0))).xyz);
			float3 n2 = normalize((mul(localNormalToWorldMatrix,
				float4(_Normals[_Indices[i + 2]], 0))).xyz);

			float t, u, v;
			if (HitTriangle(ray, v0, v1, v2, t, u, v))
			{
				if (t > 0 && t < bestHit.distance)
				{
					bestHit.distance = t;
					bestHit.position = ray.origin + t * ray.direction;
					bestHit.normal = TriLinearInterpolation(n0, n1, n2, u, v);
					bestHit.albedo = float3(0.3f,0.3f,0.3f);
					bestHit.specular = float3(1.0f, 1.0f, 1.0f);
					bestHit.smoothness = 1000.0f;
					bestHit.emission = 0.0f;
				}
			}
		}
	}

	float GetHitTime(Ray ray)
	{
		float time = 1.#INF;
		uint indices_tail = indices_offset + indices_count;
		for (uint i = indices_offset; i < indices_tail; i += 3)
		{
			float3 v0 = (mul(localToWorldMatrix,
				float4(_Vertices[_Indices[i]], 1))).xyz;
			float3 v1 = (mul(localToWorldMatrix,
				float4(_Vertices[_Indices[i + 1]], 1))).xyz;
			float3 v2 = (mul(localToWorldMatrix,
				float4(_Vertices[_Indices[i + 2]], 1))).xyz;
			float t, u, v;
			if (HitTriangle(ray, v0, v1, v2, t, u, v) && t > 0)
				time = min(time, t);
		}
		if (time > 0)
		{
			return time;
		}
		return 1.#INF;
	}
	inline static bool HitTriangle(Ray ray, float3 vert0, float3 vert1, float3 vert2,
		inout float t, inout float u, inout float v)
	{
		float3 edge1 = vert1 - vert0;
		float3 edge2 = vert2 - vert0;
		float3 pvec = cross(ray.direction, edge2);
		float det = dot(edge1, pvec);
		if (det < EPSILON)
			return false;
		float inv_det = 1.0f / det;
		float3 tvec = ray.origin - vert0;
		u = dot(tvec, pvec) * inv_det;
		if (u < 0.0 || u>1.0f)
			return false;
		float3 qvec = cross(tvec, edge1);
		v = dot(ray.direction, qvec) * inv_det;
		if (v < 0.0 || u + v >1.0f)
			return false;
		t = dot(edge2, qvec) * inv_det;
		return true;
	}
};

#endif